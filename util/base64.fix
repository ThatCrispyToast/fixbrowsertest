/*
 * FixBrowser v0.1 - https://www.fixbrowser.org/
 * Copyright (c) 2018-2024 Martin Dvorak <jezek2@advel.cz>
 *
 * This software is provided 'as-is', without any express or implied warranty.
 * In no event will the authors be held liable for any damages arising from
 * the use of this software.
 *
 * Permission is granted to anyone to use this software for any purpose, 
 * including commercial applications, and to alter it and redistribute it
 * freely, subject to the following restrictions:
 *
 * 1. The origin of this software must not be misrepresented; you must not
 *    claim that you wrote the original software. If you use this software
 *    in a product, an acknowledgment in the product documentation would be
 *    appreciated but is not required.
 * 2. Altered source versions must be plainly marked as such, and must not be
 *    misrepresented as being the original software.
 * 3. This notice may not be removed or altered from any source distribution.
 */

var @table;
var @rev_table;

function base64_encode(buf)
{
	var tbl = table;
	if (!tbl) {
		tbl = init_table();
	}

	var s = {""}, i=0;
	for (; i+2<length(buf); i+=3) {
		var value = ((buf[i] & 0xFF) << 16) | ((buf[i+1] & 0xFF) << 8) | (buf[i+2] & 0xFF);
		s[] = tbl[(value >> 18) & 0x3F];
		s[] = tbl[(value >> 12) & 0x3F];
		s[] = tbl[(value >> 6) & 0x3F];
		s[] = tbl[(value >> 0) & 0x3F];
	}

	switch (length(buf) - i) {
		case 1: {
			var b0 = buf[i] & 0xFF;
			s[] = tbl[b0 >> 2];
			s[] = tbl[(b0 << 4) & 0x3F];
			s[] = '=';
			s[] = '=';
			break;
		}

		case 2: {
			var b0 = buf[i] & 0xFF;
			var b1 = buf[i+1] & 0xFF;
			s[] = tbl[b0 >> 2];
			s[] = tbl[((b0 << 4) | (b1 >> 4)) & 0x3F];
			s[] = tbl[(b1 << 2) & 0x3F];
			s[] = '=';
			break;
		}
	}
	return s;
}

function base64_decode(buf)
{
	var tbl = rev_table;
	if (!tbl) {
		tbl = init_rev_table();
	}

	if (length(buf) & 3) {
		return 0, error("length must be multiple of 4 bytes");
	}

	var out = [];
	for (var i=0, n=length(buf)-4; i<n; i+=4) {
		var b0 = tbl[buf[i]];
		var b1 = tbl[buf[i+1]];
		var b2 = tbl[buf[i+2]];
		var b3 = tbl[buf[i+3]];
		if ((b0 | b1 | b2 | b3) & 0x80) {
			return 0, error("invalid character encountered");
		}
		out[] = (b0 << 2) | (b1 >> 4);
		out[] = ((b1 & 0xF) << 4) | (b2 >> 2);
		out[] = ((b2 & 0x3) << 6) | b3;
	}
	if (length(buf) > 0) {
		var i = length(buf)-4;
		var b0 = tbl[buf[i]];
		var b1 = tbl[buf[i+1]];
		var b2 = tbl[buf[i+2]];
		var b3 = tbl[buf[i+3]];
		out[] = (b0 << 2) | (b1 >> 4);
		if (b3 == 0x80) {
			b3 = 0;
			if (b2 == 0x80) {
				b2 = 0;
			}
			else {
				out[] = ((b1 & 0xF) << 4) | (b2 >> 2);
			}
		}
		else {
			out[] = ((b1 & 0xF) << 4) | (b2 >> 2);
			out[] = ((b2 & 0x3) << 6) | b3;
		}
		if ((b0 | b1 | b2 | b3) & 0x80) {
			return 0, error("invalid character encountered");
		}
	}
	return out;
}

function @init_table()
{
	table = [];
	array_set_length(table, 64);
	for (var i=0; i<26; i++) {
		table[i] = 'A' + i;
		table[26+i] = 'a' + i;
	}
	for (var i=0; i<10; i++) {
		table[52+i] = '0' + i;
	}
	table[62] = '+';
	table[63] = '/';
	return table;
}

function @init_rev_table()
{
	if (!table) {
		init_table();
	}

	rev_table = [];
	array_set_length(rev_table, 256);
	array_fill(rev_table, 0, length(rev_table), 0xFF);
	for (var i=0; i<64; i++) {
		rev_table[table[i]] = i;
	}
	rev_table['='] = 0x80;
	return rev_table;
}

function @assert(value, result)
{
	if (value != result) {
		return 0, error({"assert failed (expected ", result, " but got ", value, ")"});
	}
}

function @assert_base64(value, result)
{
	assert(base64_encode(value), result);
	assert(base64_decode(result), value);
}

function test_base64()
{
	assert_base64("", "");
	assert_base64("Man", "TWFu");
	assert_base64("Ma", "TWE=");
	assert_base64("M", "TQ==");
	assert_base64("any carnal pleasure.", "YW55IGNhcm5hbCBwbGVhc3VyZS4=");
	assert_base64("any carnal pleasure", "YW55IGNhcm5hbCBwbGVhc3VyZQ==");
	assert_base64("any carnal pleasur", "YW55IGNhcm5hbCBwbGVhc3Vy");
	assert_base64("any carnal pleasu", "YW55IGNhcm5hbCBwbGVhc3U=");
	assert_base64("any carnal pleas", "YW55IGNhcm5hbCBwbGVhcw==");
}
