import "util/common";
import "util/string";
import "util/template";

function is_same_domain(src, dest)
{
	if (domain_ends_with(dest, "twimg.com")) return true;
	return false;
}

function handle_request(request)
{
	var domain = request_get_domain(request);
	if (domain == "pic.twitter.com") {
		return;
	}
	if (domain != "twitter.com" || request_get_scheme(request) != "https") {
		request_set_redirect(request, {"https://twitter.com", request_get_path(request)});
		return;
	}

	var path = request_get_path(request);
	var parts = string_split(path, '/');
	if (length(parts) >= 4 && parts[2] == "status") {
		if (length(parts) > 4) {
			array_set_length(parts, 4);
			request_set_redirect(request, {"https://twitter.com", string_join(parts, "/")});
			return;
		}
		return show_status(request, parts[1]);
	}
}

function adjust_document(request, document)
{
	if (request_get_domain(request) == "pic.twitter.com") {
		var refresh = element_get_attr(element_query(document, "meta[http-equiv='refresh']"), "content");
		request_set_redirect(request, string_substring(refresh, 6));
		return;
	}

	remove_element(document, "form.NoScriptForm");

	var links = element_query_all(document, "a[href^='https://t.co/'][title^='http']");
	for (var i=0; i<length(links); i++) {
		element_set_attr(links[i], "href", element_get_attr(links[i], "title"));
	}
}

function @show_status(request, twitter_id)
{
	var s = {""};

	var content = http_get(request_get_url(request));
	var doc = document_parse(content);

	var tpl = template_create("site/com/tw/twitter.com/list.tpl");
	template_set(tpl, "title", twitter_id);

	var elems = element_query_all(doc, "div.js-tweet-text-container");
	template_begin(tpl, "posts");
	for (var i=0; i<length(elems); i++) {
		var parent = element_get_parent(elems[i]);
		var text = element_extract_text(elems[i]);
		var time = get_time(element_query(parent, "small.time > a"));
		var full_name = element_extract_text(element_query(parent, "strong.fullname"));
		var username = element_extract_text(element_query(parent, "span.username > b"));
		var image = null;

		var next_elem = element_get_next_tag(elems[i]);
		if (next_elem) {
			var photo_elem = element_query(next_elem, "div.AdaptiveMedia-container div.AdaptiveMedia-photoContainer[data-image-url]");
			if (photo_elem) {
				image = element_get_attr(photo_elem, "data-image-url");
			}
		}

		template_set(tpl, "text", html_escape(text));
		template_set(tpl, "time", html_escape(time));
		template_set(tpl, "full_name", html_escape(full_name));
		template_set(tpl, "username", html_escape(username));
		if (image) {
			template_set(tpl, "image", html_escape(image));
		}
		template_next_record(tpl);
	}
	template_end(tpl);

	return template_process(tpl);
}

function @get_time(elem)
{
	return element_get_attr(elem, "title");
}
